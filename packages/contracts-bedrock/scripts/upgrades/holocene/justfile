set dotenv-load # to have OP_CONTRACTS_RELEASE available for build-image

# Default recipe to list help menu.
default:
  @just --list

# Run the deployment / upgrade generation image. If the image is not present locally,
# it will be built.
run deploy-config-path output-folder-path="$(pwd)/output/" *args='':
  #!/bin/bash
  if [ ! "$(docker images -q op-holocene-upgrade:local 2> /dev/null)" ]; then
    just build-image
  fi

  mkdir -p {{output-folder-path}}

  # Run the deployment.
  docker run -it \
    --rm \
    -v {{output-folder-path}}:/output \
    -v {{deploy-config-path}}:/app/packages/contracts-bedrock/deploy-config/deploy-config.json \
    --env-file=.env \
    op-holocene-upgrade:local {{args}}

sys-cfg-bundle output-folder-path="$(pwd)/output/" *args='':
  #!/bin/bash
  if [ ! "$(docker images -q op-holocene-upgrade:local 2> /dev/null)" ]; then
    just build-image
  fi

  mkdir -p {{output-folder-path}}

  # Run the deployment.
  docker run -it \
    --rm \
    -v {{output-folder-path}}:/output \
    -e OUTPUT_FOLDER_PATH=/output \
    --env-file=.env \
    --entrypoint=./scripts/sys-cfg-bundle.sh \
    op-holocene-upgrade:local {{args}}

sys-cfg-task output-folder-path="$(pwd)/output/" *args='':
  #!/bin/bash
  if [ ! "$(docker images -q op-holocene-upgrade:local 2> /dev/null)" ]; then
    just build-image
  fi

  mkdir -p {{output-folder-path}}

  # Run the deployment.
  docker run -it \
    --rm \
    -v {{output-folder-path}}:/output \
    -e OUTPUT_FOLDER_PATH=/output \
    --env-file=.env \
    --entrypoint=./scripts/sc-ops-sys-cfg.sh \
    op-holocene-upgrade:local {{args}}

# Build the image locally.
build-image:
  docker build \
  -t op-holocene-upgrade:local \
  -f upgrade.dockerfile \
  --build-arg REV=op-contracts/${OP_CONTRACTS_RELEASE} \
  .
